plugins {
	id 'io.spring.dependency-management' version '1.1.7'
	id 'maven-publish'
}

// 공통 설정
allprojects {
	group = 'com.kyj'
	version = '0.0.1-SNAPSHOT'

	repositories {
		mavenCentral()
	}

	apply plugin: 'java-library'
	apply plugin: 'maven-publish'
	apply plugin: 'io.spring.dependency-management'

	java {
		withSourcesJar()
		toolchain {
			languageVersion = JavaLanguageVersion.of(17)
		}
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.3"
		}
	}

	// 공통 publishing 설정
	publishing {

		publications {
			mavenJava(MavenPublication) {
				from components.java
			}
		}
		repositories {
			maven {
				url = uri("http://100.114.34.71:9090/repository/kyj-lib-core/")
				credentials {
					username = "admin"
					password = "1234"
				}
				allowInsecureProtocol = true
			}
		}
	}

}

// 서브프로젝트 공통 설정
subprojects {
	dependencies {
		// lombok 공통 설정
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		// 테스트 의존성 공통 설정
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
		testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	}


}

// 조건부 배포를 위한 태스크 정의
task publishCore {
	dependsOn ':kyj-lib-core:publish'
	description = '코어 모듈만 배포'
}

task publishWithKafka {
	dependsOn ':kyj-lib-core:publish', ':kyj-lib-core-kafka:publish'
	description = '코어 + 카프카 모듈 배포'
}

task publishWithRedis {
	dependsOn ':kyj-lib-core:publish', ':kyj-lib-core-redis:publish'
	description = '코어 + 레디스 모듈 배포'
}

task publishWithFile {
	dependsOn ':kyj-lib-core:publish', ':kyj-lib-core-file:publish'
	description = '코어 + 파일 모듈 배포'
}

task publishWithJpa {
	dependsOn ':kyj-lib-core:publish', ':kyj-lib-core-jpa:publish'
	description = '코어 + JPA 모듈 배포'
}

task publishAll {
	dependsOn subprojects.collect { ":${it.name}:publish" }
	description = '모든 모듈 배포'
}
